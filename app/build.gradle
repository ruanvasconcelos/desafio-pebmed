plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-parcelize'
    id 'kotlin-kapt'
}

android {

    buildFeatures {
        viewBinding = true
    }

    compileSdkVersion rootProject.compile_sdk_version
    buildToolsVersion rootProject.build_tools_version

    defaultConfig {
        applicationId "com.example.desafiopebmed"
        minSdkVersion rootProject.min_sdk_version
        targetSdkVersion rootProject.compile_sdk_version
        versionCode rootProject.version_code
        versionName rootProject.version_name
        multiDexEnabled true

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += ["room.schemaLocation":
                                      "$projectDir/schemas".toString()]
            }
        }
    }

    signingConfigs {
        release {
            storeFile file(STORE_FILE_RELEASE)
            keyAlias KEY_ALIAS_RELEASE
            storePassword STORE_PASSWORD_RELEASE
            keyPassword KEY_PASSWORD_RELEASE
        }
    }

    buildTypes {
        release {
            debuggable false
            minifyEnabled true
            shrinkResources true
            zipAlignEnabled true
            buildConfigField 'String', 'HOST', '"https://private-54fe53-pebmeddesafio.apiary-mock.com/"'
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            applicationIdSuffix '.debug'
            debuggable true
            minifyEnabled false
            shrinkResources false
            zipAlignEnabled false
            buildConfigField 'String', 'HOST', '"https://private-54fe53-pebmeddesafio.apiary-mock.com/"'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttp_version"
    api "com.squareup.retrofit2:converter-gson:$retrofit_version"
    implementation "io.reactivex.rxjava3:rxjava:$rx_java_version"
    implementation "io.reactivex.rxjava3:rxandroid:$rx_android_version"
    implementation "com.squareup.retrofit2:adapter-rxjava3:$rx_adapter_version"

    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-savedstate:$lifecycle_version"

    debugImplementation "androidx.multidex:multidex:$multidex_version"

    implementation "com.google.dagger:dagger:$dagger_version"
    implementation "com.google.dagger:dagger-android-support:$dagger_version"
    kapt "com.google.dagger:dagger-compiler:$dagger_version"
    kapt "com.google.dagger:dagger-android-processor:$dagger_version"

    implementation "androidx.room:room-runtime:$arch_version"
    implementation "androidx.room:room-rxjava3:$arch_version"
    kapt "androidx.room:room-compiler:$arch_version"

    implementation "androidx.activity:activity-ktx:$activity_tx_version"

    implementation "com.github.bumptech.glide:glide:$glide_version"
    kapt "com.github.bumptech.glide:compiler:$glide_version"

    implementation "androidx.core:core-ktx:$core_ktx_version"
    implementation "androidx.appcompat:appcompat:$app_compat_version"
    implementation "com.google.android.material:material:$android_material_version"
    implementation "androidx.constraintlayout:constraintlayout:$constraint_layout_version"

    testImplementation "junit:junit:$junit_version"
    testImplementation "io.mockk:mockk:$mockK_version"
    testImplementation "com.squareup.okhttp3:mockwebserver:$okhttp_version"
    testImplementation "android.arch.core:core-testing:$arch_version"
    testImplementation "org.robolectric:robolectric:$robolectric_version"
    androidTestImplementation "androidx.test.ext:junit:$androidx_junit_version"
    androidTestImplementation "androidx.test.espresso:espresso-core:$espresso_core"
}